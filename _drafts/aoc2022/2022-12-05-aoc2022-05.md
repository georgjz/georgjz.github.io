---
layout:     post
title:      "Advent of Code 2022, Day 5"
date:       2022-12-05
excerpt:    "My solution for Advent of Code 2021 Day 5 in Racket, where we bring FORTH the elves' supplies"
tags:       [Advent of Code 2022, Racket, Scheme, Programming]
feature:    /assets/img/punchcard.jpg
published:  true
comments:   true
---
# Advent of Code 2022, Day 5

[Today's puzzle][aoc05] is rather fun! But I'm going to cheat a bit and "hardcode" the initial stack into the code.

## The Reader

Let's look at the reader:

```scheme
; The reader
(module reader racket
  (provide read-syntax)
  (require (for-syntax syntax/parse))

  (define ranges-read
    (λ (port)
      (let ((next (read-line port)))
        (if (eof-object? next)
            next
            (map string->number (cdr (regexp-split #rx"(move | to | from )" next)))))))

  (define read-syntax
    (λ (src in)
      (let* ((src           (port->list ranges-read in))
             (cmds          (map (λ (cmd) (cons 'list cmd)) src))
             (syntax-datum  `(module f supplystacksdsl
                               ,@(cdr cmds))))
        (datum->syntax #f syntax-datum)))))
```

No fany stuff here, I use regexes (ewww) again to get a simple list of numbers that represent the count, source stack and target stack indices.

## The Expander

The calculating part is pretty simple too.

```scheme
; The expander
(define-syntax supplystacksdsl-module-begin
  (λ (stx)
    (syntax-parse stx
      ((_ commands ...)
       #'(#%module-begin
           (display "\nSolution part 1: ")
           (map car (part1 initial-stacks commands ...))
           (display "\nSolution part 2: ")
           (map car (part2 initial-stacks commands ...)))))))

(define initial-stacks
  (list (list 'G 'P 'N 'R)
        (list 'H 'V 'S 'C 'L 'B 'J 'T)
        (list 'L 'N 'M 'B 'D 'T)
        (list 'B 'S 'P 'V 'R)
        (list 'H 'V 'M 'W 'S 'Q 'C 'G)
        (list 'J 'B 'D 'C 'S 'Q 'W)
        (list 'L 'Q 'F)
        (list 'V 'F 'L 'D 'T 'H 'M 'W)
        (list 'F 'J 'M 'V 'B 'P 'L)))

(define crate-move
  (λ (f)
    (λ (stacks cnt src dst)
      (let* ((src-stack  (list-ref stacks (sub1 src)))
             (dst-stack  (list-ref stacks (sub1 dst)))
             (crates     (f (take src-stack cnt)))
             (new-src    (drop src-stack cnt))
             (new-dst    (append crates dst-stack)))
        (list-set (list-set stacks (sub1 src) new-src)
                  (sub1 dst)
                  new-dst)))))

(define crate-move-9000 (crate-move reverse))

(define crate-move-9001 (crate-move identity))

(define solution
  (λ (move)
    (λ (supplies cmd . cmds)
      (let* ((cnt           (car cmd))
             (src           (cadr cmd))
             (dst           (caddr cmd))
             (new-supplies  (move supplies cnt src dst)))
        (cond
          ((null? cmds)  new-supplies)
          (else          (apply (solution move) (cons new-supplies cmds))))))))

(define part1 (solution crate-move-9000))

(define part2 (solution crate-move-9001))
```


You can find all my solutions on [my GitHub][gh].

See you [tomorrow][day06].


[br]: https://beautifulracket.com
[aoc]: https://adventofcode.com/2022
[aoc05]: https://adventofcode.com/2022/day/5
[hask]: https://wiki.haskell.org/Haskell
[scm]: https://en.wikipedia.org/wiki/Scheme_(programming_language)
[chez]: https://cisco.github.io/ChezScheme/
[ghc]: https://www.haskell.org/ghc/
[apl]: https://en.wikipedia.org/wiki/APL_(programming_language)
[rkt]: https://racket-lang.org
[forth]: https://en.wikipedia.org/wiki/Forth_(programming_language)
[dsl]: https://en.wikipedia.org/wiki/Domain-specific_language
[gh]: https://github.com/georgjz/advent-of-code-2022
[input]: https://github.com/georgjz/advent-of-code-2021/blob/main/Day_01_Sonar_Sweep/Input.txt
[comp]: https://en.wikipedia.org/wiki/Function_composition_(computer_science)
[pure]: https://en.wikipedia.org/wiki/Pure_function
[day06]: {% post_url aoc2022/2022-12-06-aoc2022-06 %}