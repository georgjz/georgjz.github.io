---
layout:     post
title:      "Advent of Code 2022, Day 4"
date:       2022-12-04
excerpt:    "My solution for Advent of Code 2021 Day 4 in Racket, where we make the elves clean camp"
tags:       [Advent of Code 2022, Racket, Scheme, Programming]
feature:    /assets/img/punchcard.jpg
published:  true
comments:   true
---
# Advent of Code 2022, Day 4

[Today's puzzle][aoc04] is rather easy.

## The Reader

I'm going to forget my aversion against regexes for a moment (to be fair, Racket does them somewhat decent - still, death to regex), and use them in this easy reader:

```scheme
; The reader
(module reader racket
  (provide read-syntax)
  (require (for-syntax syntax/parse))

  (define ranges-read
    (λ (port)
      (let ((next (read-line port)))
        (if (eof-object? next)
            next
            (map string->number (regexp-split #rx"[-,]" next))))))

  (define read-syntax
    (λ (src in)
      (let* ((ranges        (port->list ranges-read in))
             (src           (map (λ (range) (cons 'list range)) (cdr ranges)))
             (syntax-datum  `(module f campcleanupdsl
                               ,@src)))
        (datum->syntax #f syntax-datum)))))
```

The magic happens in `regexp-split`, where we pass a regex `[-,]` that will remove the hypens and comma from each line. The four number strings are then converted into proper numbers and that's it.

## The Expander

The expander is also super easy, just call the solution functions on the input provided by the reader:

```scheme
; The expander
(define-syntax campcleanupdsl-begin-module
  (λ (stx)
    (syntax-parse stx
      ((_ a ...)
       #'(#%module-begin
           (display "\nSolution part 1: ")
           (part1 a ...)
           (display "\nSolution part 2: ")
           (part2 a ...))))))
```

To calculate the solutions, we simply define two predicate functions that check whether two ranges intersect, or are subsets of each other, respectively:

```scheme
(define sub-range?
  (λ (a b c d)
    (let ((set1 (iota (add1 (- b a)) a 1))
          (set2 (iota (add1 (- d c)) c 1)))
      (or (subset? set1 set2) (subset? set2 set1)))))

(define intersect?
  (λ (a b c d)
    (let ((set1 (iota (add1 (- b a)) a 1))
          (set2 (iota (add1 (- d c)) c 1)))
      (not (empty? (set-intersect set1 set2))))))
```

Then calculating the actual solutions becomes ye olde fold-with-predicate-and-count:

```scheme
(define solution
  (λ (pred?)
    (λ bs
      (foldl (λ (a b) (if (apply pred? a) (+ 1 b) b))
            0
            bs))))

(define part1 (solution sub-range?))

(define part2 (solution intersect?))
```

You can find all my solutions on [my GitHub][gh].

See you [tomorrow][day05].


[br]: https://beautifulracket.com
[aoc]: https://adventofcode.com/2022
[aoc04]: https://adventofcode.com/2022/day/4
[hask]: https://wiki.haskell.org/Haskell
[scm]: https://en.wikipedia.org/wiki/Scheme_(programming_language)
[chez]: https://cisco.github.io/ChezScheme/
[ghc]: https://www.haskell.org/ghc/
[apl]: https://en.wikipedia.org/wiki/APL_(programming_language)
[rkt]: https://racket-lang.org
[forth]: https://en.wikipedia.org/wiki/Forth_(programming_language)
[dsl]: https://en.wikipedia.org/wiki/Domain-specific_language
[gh]: https://github.com/georgjz/advent-of-code-2022
[input]: https://github.com/georgjz/advent-of-code-2021/blob/main/Day_01_Sonar_Sweep/Input.txt
[comp]: https://en.wikipedia.org/wiki/Function_composition_(computer_science)
[pure]: https://en.wikipedia.org/wiki/Pure_function
[day05]: {% post_url aoc2022/2022-12-05-aoc2022-05 %}