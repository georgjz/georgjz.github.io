---
layout:     post
title:      "Advent of Code 2022, Day 2"
date:       2022-12-02
excerpt:    "My solution for Advent of Code 2021 Day 2 in Racket, where we battle a bunch of elves"
tags:       [Advent of Code 2022, Racket, Scheme, Programming]
feature:    /assets/img/punchcard.jpg
published:  true
comments:   true
---
# Advent of Code 2022, Day 2

Hohoho, [Advent of Code][aoc] is upon us! Solve two programming problems every day, because that's fun!

[Today's puzzle][aoc02] is not super hard. It could have been done more elegantly with some sort of `cycle` function - but I didn't bother. Just plain brute force (I find these kind of puzzles boring - it's essentially one big switch statement to cover all cases. meh.)

The reader is easy enough:

```scheme
; The reader
(module reader racket
  (provide read-syntax)
  (require (for-syntax syntax/parse))

  (define read-hands
    (λ (port)
      (let* ((next  (read-char port)))
        (when (or (eof-object? next) (char-alphabetic? next))
          next))))

  (define read-syntax
    (λ (src in)
      (let* ((hands         (filter-not void? (port->list read-hands in)))
             (syntax-datum  `(module f rockpaperscissorsdsl
                               ,@hands)))
        (datum->syntax #f syntax-datum)))))
```

Next, I expand the list of hands/plays:

```scheme
; expander
(define-syntax rps-module-begin
  (λ (stx)
    (syntax-parse stx
      ((_ hand ...)
       #'(#%module-begin
           (let ((plays (split-twos hand ...)))
             ; FIXME: why?
             (print (part1 plays))
             (part2 plays)))))))
```

Finally, a few function to calculate the results. As I said, just a big switch statement essentially:

```scheme
; solutions
(define part1
  (λ (plays)
    (display "\nSolution part 1: ")
    (apply + (map (λ (x) (apply score x)) plays))))

(define part2
  (λ (plays)
    (display "\nSolution part 2: ")
    (apply + (map (λ (x) (apply match-strategy x)) plays))))

; split
(define split-twos
  (λ (opp me . rest)
    (if (null? rest)
        (list (list opp me))
        (cons (list opp me)
              (apply split-twos rest)))))

; scoring functions
(define score
  (λ (opp me)
    (+ (hand-score me) (result-score opp me))))

(define hand-score
  (λ (hand)
    (match hand
      (#\A  1)  (#\X  1)
      (#\B  2)  (#\Y  2)
      (#\C  3)  (#\Z  3))))

(define result-score
  (λ (op me)
    (match (list op me)
      ((list #\A #\Y)  6)
      ((list #\C #\X)  6)
      ((list #\B #\Z)  6)
      ((list #\B #\Y)  3)
      ((list #\C #\Z)  3)
      ((list #\A #\X)  3)
      (else            0))))

(define match-strategy
  (λ (opp strategy)
    (let* ((winner (λ (hand)
                     (match hand
                       (#\A  #\Y)
                       (#\B  #\Z)
                       (#\C  #\X))))
           (loser (λ (hand)
                    (match hand
                      (#\A  #\Z)
                      (#\B  #\X)
                      (#\C  #\Y)))))
      (match strategy
        (#\Z (+ 6 (hand-score (winner opp))))  ; win
        (#\Y (+ 3 (hand-score opp)))  ; draw
        (#\X (+ 0 (hand-score (loser opp))))))))  ; lose
```

That's it for boring today. You can find all my solutions on [my GitHub][gh].

See you [tomorrow][day03].


[br]: https://beautifulracket.com
[aoc]: https://adventofcode.com/2022
[aoc02]: https://adventofcode.com/2022/day/2
[hask]: https://wiki.haskell.org/Haskell
[scm]: https://en.wikipedia.org/wiki/Scheme_(programming_language)
[chez]: https://cisco.github.io/ChezScheme/
[ghc]: https://www.haskell.org/ghc/
[apl]: https://en.wikipedia.org/wiki/APL_(programming_language)
[rkt]: https://racket-lang.org
[forth]: https://en.wikipedia.org/wiki/Forth_(programming_language)
[dsl]: https://en.wikipedia.org/wiki/Domain-specific_language
[gh]: https://github.com/georgjz/advent-of-code-2022
[input]: https://github.com/georgjz/advent-of-code-2021/blob/main/Day_01_Sonar_Sweep/Input.txt
[comp]: https://en.wikipedia.org/wiki/Function_composition_(computer_science)
[pure]: https://en.wikipedia.org/wiki/Pure_function
[day03]: {% post_url aoc2022/2022-12-03-aoc2022-03 %}