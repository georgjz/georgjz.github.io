---
layout:     post
title:      "Advent of Code 2022, Day 1"
date:       2022-12-01
excerpt:    "My solution for Advent of Code 2022 Day 1 in Racket"
tags:       [Advent of Code 2022, Racket, Scheme, Programming]
feature:    /assets/img/punchcard.jpg
published:  true
comments:   true
---
# Advent of Code 2022, Day 1

Hohoho, [Advent of Code][aoc] is upon us! Solve two programming problems every day, because that's fun!

As a scheming [Haskeller][hask], and a haskelling [Schemer][scm], I'm going to use...[Racket][rkt]. The solutions will be on [my Github][gh]. I'll assume you know a bit about how Racket is used to create new languages. If you're new to this, you may want to check out [Beautiful Racket][br], an excellent book on how to write your own languages.

This year I want to do things a bit differently. Instead of finding the shortest (that's what [APL][apl] and [Forth][forth] are for), fastest, or most elegant solution (still [APL][apl]), I'm going to look at the problem as a compiler design problem. That is, I'll be treading the input as some sort of [domain specific language][dsl]. Then solving the puzzle becomes the task of writing a interpreter/compiler for said language.

That may not be the most efficient way to get to a solution, but I think it's an interesting exercise. Let's see how far it can take me. Mind that most of my solutions will come with the advantage of hindsight. That is, I rework my solutions (aka, the compiler) with knowledge what the second part of each puzzle requires.

## Translating the Input/DSL to Racket

Let's first look at the form the input of our compiler takes. According to the [description of part 1][aoc01] (the input won't change for part 2), the input is a list of calories a bunch of elves are carrying:

>The Elves take turns writing down the number of Calories contained by the various meals, snacks, rations, etc. that they've brought with them, one item per line. Each Elf separates their own inventory from the previous Elf's inventory (if any) by a blank line.

```
1000
2000
3000

4000

5000
6000

7000
8000
9000

10000
```

So this program describes five elves carrying items worth 6000, 4000, 11000, 24000, and 10000 calories respectively. What we need to do is to translate this problem into a Racket program that looks like this:

```scheme
'(6000 4000 11000 24000 10000)
```

That is, a list with the total calories each elve is carrying. Based on that, calculating the solution to part 1:

>Find the Elf carrying the most Calories. How many total Calories is that Elf carrying?

becomes

```scheme
(max 6000 4000 11000 24000 10000)
```

If we "reverse-expand" this further, we actually get

```scheme
(max (+ 1000 2000 3000)
     (+ 4000)
     (+ 5000 6000)
     (+ 7000 8000 9000)
     (+ 10000))
```

Let's see how we get there with a Racket reader.

### The Reader for `countingcaloriesdsl`

The reader will simply read the input file line by line. Once all lines are read in, the elves' calories will separated by `""` (i.e., the empty string). We use the function `endby` to group all calories of each elve into its own sublist. Finally, we prefix each list with `elve-calories`, which will be a function that calculates the total calories one elve is carrying.

```scheme
; The reader
(module reader racket
  (provide read-syntax)
  (require (for-syntax syntax/parse))

  (define endby
    (λ (ls del)
      (foldr (λ (elem col)
               (if (equal? elem del)
                   (cons empty col)
                   (cons (cons elem (car col))
                         (cdr col))))
             (list empty)
             ls)))

  (define my-read-line
    (λ (port)
      (let* ((next    (read-line port))
            (number   (unless (eof-object? next) (string->number next))))
        (if (number?  number)
            number
            next))))

    (define read-syntax
      (λ (src in)
        (let* ((src          (port->list my-read-line in))
               (elves         (map (λ (ls) (cons 'elve-calories ls)) (endby (cdr src) "")))
               (syntax-datum  `(module f countingcaloriesdsl
                                 (part1 ,@elves)
                                 (part2 ,@elves))))
          (datum->syntax #f syntax-datum)))))
```

This reader will ultimately output a program of the form:

```scheme
(part1 (elve-calories 1000 2000 3000)
       (elve-calories 4000)
       (elve-calories 5000 6000)
       (elve-calories 7000 8000 9000)
       (elve-calories 10000))
(part2 (elve-calories 1000 2000 3000)
       (elve-calories 4000)
       (elve-calories 5000 6000)
       (elve-calories 7000 8000 9000)
       (elve-calories 10000))
```

So we need to define `part1`, `part2`, and `elve-calories` next.

`elve-calories` simply calculates the sum of all values passed to it:

```scheme
; Calculate the total calories of a given elve
(define elve-calories (λ x (apply + x)))
```

Easy. Next, part one asks us to find the largest amount of calories one elve is carrying, which as pointed out earlier, is simply the maximum of the given list of calories:

```scheme
; Result part 1
(define part1
  (λ x
    (display "\nSolution part 1: ")
    (apply max x)))
```

Finally, part two asks us to

>Find the top three Elves carrying the most Calories. How many Calories are those Elves carrying in total?

which means we simply sort the list, take the top three elements, and calculate their sum:

```scheme
; Result part 2
(define part2
  (λ x
    (display "\nSolution part 2: ")
    (apply + (take (sort x >) 3))))
```

For the test input, this yields:

```
$ racket Test_Input.rkt

Solution part 1: 24000

Solution part 2: 45000
```

which is the correct answer.

That's it for today. You can find all my solutions on [my Github][gh].

See you [tomorrow][day02].

[br]: https://beautifulracket.com
[aoc]: https://adventofcode.com/2022
[aoc01]: https://adventofcode.com/2022/day/1
[hask]: https://wiki.haskell.org/Haskell
[scm]: https://en.wikipedia.org/wiki/Scheme_(programming_language)
[chez]: https://cisco.github.io/ChezScheme/
[ghc]: https://www.haskell.org/ghc/
[apl]: https://en.wikipedia.org/wiki/APL_(programming_language)
[rkt]: https://racket-lang.org
[forth]: https://en.wikipedia.org/wiki/Forth_(programming_language)
[dsl]: https://en.wikipedia.org/wiki/Domain-specific_language
[gh]: https://github.com/georgjz/advent-of-code-2022
[comp]: https://en.wikipedia.org/wiki/Function_composition_(computer_science)
[pure]: https://en.wikipedia.org/wiki/Pure_function
[day02]: {% post_url aoc2022/2021-12-02-aoc2022-02 %}