---
layout:     post
title:      "Advent of Code 2021, Day 4"
date:       2021-12-04
excerpt:    "For Advent of Code 2021 Day 4, where battle a giant squid in bingo"
tags:       [Advent of Code 2021, Haskell, Programming]
feature:    /assets/img/punchcard.jpg
published:  true
comments:   true
---
# Advent of Code 2021, Day 4

Welcome to [Day 4][aoc04] of [Advent of Code 2021][aoc].

Our submarine adventure continues, but it's getting boring so we're gonna play some bingo with a giant squid.

<blockquote class="imgur-embed-pub" lang="en" data-id="TvLdoq2" data-context="false" ><a href="//imgur.com/TvLdoq2"></a></blockquote><script async src="//s.imgur.com/min/embed.js" charset="utf-8"></script>

That's the only SFW squid gif I could find. Without further ado, here we go.

Since I was lazy [yesterday][day03], I'll be lazy again today, but in Haskell.

## Haskell

We're going to be a bit fancy today and write a simple parser with [megaparsec][megaparsec]:

```haskell
data NavCom
    = Forward Integer
    | Up Integer
    | Down Integer
    deriving (Eq, Show)

type Position = (Integer, Integer, Integer)

-- Parser
type Parser = Parsec Void String

sc :: Parser ()
sc = L.space space1 empty empty

lexeme :: Parser a -> Parser a
lexeme = L.lexeme sc

integer :: Parser Integer
integer = lexeme L.decimal

navcom :: Parser NavCom
navcom = operator <*> integer
    where operator = lexeme $ choice [ Forward <$ string' "forward"
                                     , Up      <$ string' "up"
                                     , Down    <$ string' "down" ]

navcoms :: Parser [NavCom]
navcoms = manyTill navcom eof
```

First, we use some [algebraic datatypes](https://wiki.haskell.org/Algebraic_data_type) to define our own types. This will come in handy in a moment when we use pattern matching. Navigation commands are its own type, positions will be represented as triples.

The parser itself, `navcoms`, is pretty simple. We use a space consumer `sc` that will take care of all whitespace. `lexeme` is a helper function that will remove all whitespace *after* a token has been matched. `navcom` finally puts it all together: We choose the correct type constructor by matching the input with either one of the strings `forward`, `up`, or `down`, and apply that to the `integer` parser who will parse the integer after the just matched string.

As always, we hide the nasty impure input the `IO ()` monad `main`:

```haskell
main :: IO ()
main = do
    handle <- openFile "Input.txt" ReadMode
    contents <- hGetContents handle
    let (Right input) = runParser navcoms "" contents
    print $ "Part 1: " ++ show (part1 input)
    print $ "Part 2: " ++ show (part2 input)
    hClose handle
```

Only thing missing now are the `part1` and `part2` functions.

### Part 1

We'll use a similar approach as with the Scheme solution. We define a few simple functions and compose them into a solution:

```haskell
moveSimple :: NavCom -> Position -> Position
moveSimple (Forward val) (horizontal, depth, aim) = (horizontal + val, depth, aim)
moveSimple (Up val)      (horizontal, depth, aim) = (horizontal, depth - val, aim)
moveSimple (Down val)    (horizontal, depth, aim) = (horizontal, depth + val, aim)

navigate :: (NavCom -> Position -> Position) -> [NavCom] -> Position
navigate move = foldr move (0, 0, 0)

solution :: (NavCom -> Position -> Position) -> [NavCom] -> Integer
solution movef navcoms =
    let navcoms' = reverse navcoms
        (horizontal, depth, _) = navigate movef navcoms'
    in horizontal * depth

part1 :: [NavCom] -> Integer
part1 = solution moveSimple
```

Here, we use pattern matching over the types we defined earlier. Then we use a simple `foldr` over the list of navigation commands to get the final position in `navigate`. That result is then used in `solution` to again calculate the product of the horizontal position and the depth. We can compose everything in `part1`. Take note of the [eta-reduction](https://wiki.haskell.org/Eta_conversion) applied to `navigate` and `part1`.

### Part 2

We can reuse our solution from part 1:

```haskell
moveAim :: NavCom -> Position -> Position
moveAim (Forward val) (horizontal, depth, aim) = (horizontal + val, depth + aim * val, aim)
moveAim (Up val)      (horizontal, depth, aim) = (horizontal, depth, aim - val)
moveAim (Down val)    (horizontal, depth, aim) = (horizontal, depth, aim + val)

part2 :: [NavCom] -> Integer
part2 = solution moveAim
```

We again only change the *move function* we pass to `solution` to get the correct result:

```bash
$ ghci .\Dive_Bang.hs
GHCi, version 9.0.1: https://www.haskell.org/ghc/  :? for help
[1 of 1] Compiling DiveBang         ( Dive_Bang.hs, interpreted )
Ok, one module loaded.
ghci> main
"Part 1: 1815044"
"Part 2: 1739283308"
ghci>
```

Again, the correct numbers [for my input][input].

That's it for today. You can find all my solutions on [my Github][gh].

See you [tomorrow][day05].


[aoc]: https://adventofcode.com/2021
[aoc04]: https://adventofcode.com/2021/day/4
[hask]: https://wiki.haskell.org/Haskell
[scm]: https://en.wikipedia.org/wiki/Scheme_(programming_language)
[chez]: https://cisco.github.io/ChezScheme/
[ghc]: https://www.haskell.org/ghc/
[gh]: https://github.com/georgjz/advent-of-code-2021
[input]: https://github.com/georgjz/advent-of-code-2021/blob/main/Day_04_Giant_Squid/Input.txt
[comp]: https://en.wikipedia.org/wiki/Function_composition_(computer_science)
[pure]: https://en.wikipedia.org/wiki/Pure_function
[scms]: https://github.com/georgjz/advent-of-code-2021/blob/main/Day_04_Giant_Squid/Giant_Squid.scm
[hs]: https://github.com/georgjz/advent-of-code-2021/blob/main/Day_04_Giant_Squid/Giant_Squid.hs
[megaparsec]: https://hackage.haskell.org/package/megaparsec
[day03]: {% post_url aoc2021/2021-12-03-aoc202103 %}
[day05]: {% post_url aoc2021/2021-12-05-aoc202105 %}